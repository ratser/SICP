#lang r5rs
(#%require "error.rkt")
(#%require "table.rkt")

;;; table for installing eval procedures of special form
(define special-form-table (make-anyD-table))
(define (put-eval tag proc)
  ((special-form-table 'insert!) (list tag) proc))
(define (get-eval tag)
  ((special-form-table 'lookup) (list tag)))

;;; eval procedure, named my-eval to distinguish it
;;; from the build in eval procedure
(define (my-eval exp env)
  (cond ((self-evaluating? exp) exp)
        ((variable? exp) (lookup-variable-value exp env))
        ((get-eval (get-tag exp))
         ((get-eval (get-tag exp)) exp env))
        ((application? exp)
         (my-apply (my-eval (operator exp) env)
                   (list-of-values (operands exp) env)))
        (else
         (error "Unknown expression type -- MY-EVAL" exp))))

;;; apply procedure, named my-apply to distinguish it
;;; from the build in apply procedure
(define (my-apply proc argument-list)
  (cond ((primitive-procedure? proc)
         (apply-primitive-procedure proc argument-list))
        ((compound-procedure? proc)
         (eval-sequence
          (procedure-body proc)
          (extend-environment
           (procedure-parameters proc)
           argument-list
           (procedure-environment proc))))
        (else
         (error "Unknown procedure type -- MY-APPLY" proc))))

;;; procedures for my-apply
(define (list-of-values exps env)
  (if (no-operands? exps)
      '()
      (cons (my-eval (first-operand exps) env)
            (list-of-values (rest-operands exps) env))))
(define (eval-sequence exps env)
  (cond ((last-exp? exps) (my-eval (first-exp exps) env))
        (else (my-eval (first-exp exps) env)
              (eval-sequence (rest-exps exps) env))))

;;; define eval procedures for special forms
(define (eval-if exp env)
  (if (true? (my-eval (if-predicate exp) env))
      (my-eval (if-consequent exp) env)
      (my-eval (if-alternative exp) env)))
(define (eval-assignment exp env)
  (set-variable-value! 
   (assignment-variable exp)
   (my-eval (assignment-value exp) env)
   env))
(define (eval-definition exp env)
  (define-variable!
    (definition-variable exp)
    (my-eval (definition-value exp) env)
    env))
(define (eval-quoted exp env)
  (text-of-quotation exp))
(define (eval-begin exp env)
  (eval-sequence (begin-actions exp) env))
(define (eval-lambda exp env)
  (make-procedure (lambda-parameters exp)
                  (lambda-body exp)
                  env))
(define (eval-cond exp env)
  (my-eval (cond->if exp) env))
(define (eval-or exp env)
  (my-eval (or->if exp) env))
(define (eval-and exp env)
  (my-eval (and->if exp) env))
(define (eval-let exp env)
  (my-eval (let->combination exp) env))
(define (eval-let* exp env)
  (my-eval (let*->nested-lets exp) env))

;;; install eval procedures for special forms
(put-eval 'if eval-if)
(put-eval 'set! eval-assignment)
(put-eval 'define eval-definition)
(put-eval 'quote eval-quoted)
(put-eval 'begin eval-begin)
(put-eval 'lambda eval-lambda)
(put-eval 'cond eval-cond)
(put-eval 'or eval-or)
(put-eval 'and eval-and)
(put-eval 'let eval-let)
(put-eval 'let* eval-let*)

;;; represent expressions
(define (get-tag exp) (car exp))
(define (self-evaluating? exp)
  (cond ((number? exp) #t)
        ((string? exp) #t)
        (else #f)))
(define (variable? exp)
  (symbol? exp))
(define (text-of-quotation exp)
  (cadr exp))
(define (if-predicate exp) (cadr exp))
(define (if-consequent exp) (caddr exp))
(define (if-alternative exp)
  (if (not (null? (cdddr exp)))
      (cadddr exp)
      'false))
(define (make-if predicate consequent alternative)
  (list 'if predicate consequent alternative))
(define (assignment-variable exp)
  (cadr exp))
(define (assignment-value exp)
  (caddr exp))
(define (definition-variable exp)
  (if (symbol? (cadr exp))
      (cadr exp)
      (caadr exp)))
(define (definition-value exp)
  (if (symbol? (cadr exp))
      (caddr exp)
      (make-lambda (cdadr exp) (cddr exp))))
(define (lambda-parameters exp)
  (cadr exp))
(define (lambda-body exp)
  (cddr exp))
(define (make-lambda parameters body)
  (cons 'lambda (cons parameters body)))
(define (begin-actions exp)
  (cdr exp))
(define (last-exp? seq)
  (null? (cdr seq)))
(define (first-exp seq)
  (car seq))
(define (rest-exps seq)
  (cdr seq))
(define (seq->exp seq)
  (cond ((null? seq) seq)
        ((last-exp? seq) (first-exp seq))
        (else (make-begin seq))))
(define (make-begin seq)
  (cons 'begin seq))
(define (application? exp) (pair? exp))
(define (operator exp) (car exp))
(define (operands exp) (cdr exp))
(define (no-operands? ops) (null? ops))
(define (first-operand ops) (car ops))
(define (rest-operands ops) (cdr ops))

;;; derived expressions
(define (cond-clauses exp) (cdr exp))
(define (cond-else-clause? clause)
  (eq? (cond-predicate clause) 'else))
(define (cond-predicate clause)
  (car clause))
(define (cond-actions clause) (cdr clause))
(define (cond=>clause? clause)
  (eq? (cadr clause) '=>))
(define (cond-func clause)
  (caddr clause))
(define (cond->if exp)
  (expand-clauses (cond-clauses exp)))
(define (expand-clauses clauses)
  (if (null? clauses)
      'false
      (let ((first (car clauses))
            (rest (cdr clauses)))
        (cond ((cond-else-clause? first)
               (if (null? rest)
                   (seq->exp (cond-actions first))
                   (error "ELSE clause isn't last -- COND->IF"
                          clauses)))
              ((cond=>clause? first)
               (make-if
                (cond-predicate first)
                (cons (cond-func first) 
                      (cons (cond-predicate first) '()))
                (expand-clauses rest)))
              (else (make-if
                     (cond-predicate first)
                     (seq->exp (cond-actions first))
                     (expand-clauses rest)))))))
(define (or-seq exp)
  (cdr exp))
(define (or->if exp)
  (expand-or (or-seq exp)))
(define (expand-or seq)
  (if (last-exp? seq)
      (first-exp seq)
      (make-if
       (first-exp seq)
       (first-exp seq)
       (expand-or (rest-exps seq)))))
(define (and-seq exp)
  (cdr exp))
(define (and->if exp)
  (expand-and (and-seq exp)))
(define (expand-and seq)
  (if (last-exp? seq)
      (first-exp seq)
      (make-if
       (first-exp seq)
       (expand-and (rest-exps seq))
       'false)))
(define (let-variables exp)
  (map car (cadr exp)))
(define (let-exps exp)
  (map cadr (cadr exp)))
(define (let-body exp)
  (cddr exp))
(define (let->combination exp)
  (cons
   (make-lambda (let-variables exp)
                (let-body exp))
   (let-exps exp)))
(define (make-let bindings body)
  (cons 'let (cons bindings body)))
(define (let*-bindings exp)
  (cadr exp))
(define (first-binding bindings)
  (list (car bindings)))
(define (rest-bindings bindings)
  (cdr bindings))
(define (last-binding? bindings)
  (null? (cdr bindings)))
(define (let*-body exp)
  (cddr exp))
(define (let*->nested-lets exp)
  (let*-expand (let*-bindings exp)
               (let*-body exp)))
(define (let*-expand bindings body)
  (if (last-binding? bindings)
      (make-let bindings body)
      (make-let (first-binding bindings)
                (list (let*-expand (rest-bindings bindings)
                             body)))))

;;; Evaluator data structure ;;;
(define (true? x)
  (not (eq? x 'false)))
(define (false? x)
  (eq? x 'false))

(define (make-procedure parameters body env)
  (list 'procedure parameters body env))
(define (compound-procedure? p)
  (tagged-list? p 'procedure))
(define (tagged-list? p tag)
  (if (pair? p)
      (eq? (car p) tag)
      #f))
(define (procedure-parameters p) (cadr p))
(define (procedure-body p) (caddr p))
(define (procedure-environment p) (cadddr p))

;;; implement environment ;;;
(define (enclosing-environment env) (cdr env))
(define (first-frame env) (car env))
(define the-empty-environment '())
(define (make-frame variables values)
  (cons variables values))
(define (frame-variables frame)
  (car frame))
(define (frame-values frame)
  (cdr frame))
(define (add-binding-to-frame! var val frame)
  (set-car! frame (cons var (car frame)))
  (set-cdr! frame (cons val (cdr frame))))
(define (extend-environment vars vals base-env)
  (if (= (length vars) (length vals))
      (cons (make-frame vars vals) base-env)
      (error "The number of arguments and values is mismatched")))
(define (lookup-variable-value var env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars)
             (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
             (car vals))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        (error "Unbound variable" var)
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)
                (frame-values frame)))))
  (env-loop env))
(define (set-variable-value! var val env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars)
             (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
             (set-car! vals val))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        (error "Unbound variable -- SET!" var)
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)
                (frame-values frame)))))
  (env-loop env))
(define (define-variable! var val env)
  (let ((frame (first-frame env)))
    (define (scan vars vals)
      (cond ((null? vars)
             (add-binding-to-frame! var val frame))
            ((eq? var (car vars))
             (set-car! vals val))
            (else (scan (cdr vars) (cdr vals)))))
    (scan (frame-variables frame)
          (frame-values frame))))

;;;set up global environment with primitives;;;
(define (setup-environment)
  (let ((initial-env
         (extend-environment (primitive-procedure-names)
                             (primitive-procedure-objects)
                             the-empty-environment)))
    (define-variable! 'true 'ture initial-env)
    (define-variable! 'false 'false initial-env)
    initial-env))

(define (primitive-procedure? proc)
  (tagged-list? proc 'primitive))
(define (primitive-implementation proc) (cadr proc))
(define (bool-convert f)
  (lambda x
    (if (apply f x)
        'true
        'false)))
(define primitive-procedures
  (list (list 'car car)
        (list 'cdr cdr)
        (list 'cons cons)
        (list 'null? (bool-convert null?))
        (list 'pair? (bool-convert pair?))
        (list 'number? (bool-convert number?))
        (list 'symbol? (bool-convert symbol?))
        (list 'eq? (bool-convert eq?))
        (list 'list list)
        (list '= (bool-convert =))
        (list '> (bool-convert >))
        (list '< (bool-convert <))
        (list '+ +)
        (list '- -)
        (list '* *)
        (list '/ /)
        (list 'remainder remainder)))
(define (primitive-procedure-names)
  (map car primitive-procedures))
(define (primitive-procedure-objects)
  (map (lambda (proc) (list 'primitive (cadr proc)))
       primitive-procedures))
(define (apply-primitive-procedure proc args)
  (apply (primitive-implementation proc) args))

;;;prompt interface;;;
(define input-prompt "my-eval input:")
(define output-prompt "my-eval value:")
(define (driver-loop)
  (prompt-for-input input-prompt)
  (let ((input (read)))
    (let ((output (my-eval input the-global-environment)))
      (announce-output output-prompt)
      (user-print output)))
  (driver-loop))
(define (prompt-for-input string)
  (newline)
  (display string)
  (newline))
(define (announce-output string)
  (newline)
  (display string)
  (newline))
(define (user-print object)
  (if (compound-procedure? object)
      (display (list 'compound-procedure
                     (procedure-parameters object)
                     (procedure-body object)
                     '<procedure-env>))
      (display object)))

(define the-global-environment (setup-environment))
(driver-loop)

    
  
